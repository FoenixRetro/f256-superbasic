1000 '
1010 ' "Simple Space Invaders Game"
1020 '
1030 cls:bitmap on:sprites on:bitmap clear 0
1040 defineVariables()
1050 resetlevel():resetPlayer()
1060 repeat
1070 if event(moveInvadersEvent,invaderSpeed) then moveInvaders()
1080 if event(movePlayerEvent,3) then movePlayer()
1090 until false
1100 end:
1110 '
1120 ' "Move the player"
1130 '
1140 proc movePlayer()
1150 xPlayer = min(304,max(16,xPlayer+joyx(0)<<2))
1160 sprite 63 image 6 to xPlayer,220
1170 endproc
1180 '
1190 ' "Move invaders across/down"
1200 '
1210 proc moveInvaders()
1220 xInvaders = xInvaders + xiInvaders
1230 if xInvaders < leftEdge
1240 xInvaders = leftEdge
1250 yInvaders = yInvaders + abs(xiInvaders)
1260 xiInvaders = -xiInvaders
1270 endif
1280 if xInvaders > rightEdge
1290 xInvaders = rightEdge
1300 yInvaders = yInvaders + abs(xiInvaders)
1310 xiInvaders = -xiInvaders
1320 endif
1330 drawInvaders(xInvaders,yInvaders)
1340 endproc
1350 '
1360 ' "Draw invaders sprites at correct position"
1370 '
1380 proc drawInvaders(xPos,yPos)
1390 local x,y,s
1400 altGraphic = 1 - altGraphic
1410 for x = 0 to 7
1420 s = x * 5
1430 if colHeight(x) > 0
1440 for y = 0 to colHeight(x)-1
1450 sprite s image graphic(y)+altGraphic to xPos+x*24,yPos+y*24
1460 s = s + 1
1470 next
1480 endif
1490 next
1500 endproc
1510 '
1520 ' "Set up variables"
1530 '
1540 proc defineVariables()
1550 local i
1560 dim colheight(7),graphic(4)
1570 for i = 0 to 4:graphic(i) = i % 3 * 2:next:altGraphic = 0
1580 endproc
1590 '
1600 ' "Reset the player"
1610 '
1620 proc resetPlayer()
1630 xPlayer = 160
1640 endproc
1650 '
1660 ' "Set up new level"
1670 '
1680 proc resetlevel()
1690 local i
1700 for i = 0 to 7:colHeight(i) = 5:next
1710 invTotal = 5*8
1720 xInvaders = 160-7*12:yInvaders = 26:xiInvaders = 8
1730 drawInvaders(xInvaders,yInvaders)
1740 invaderSpeed = 4+invTotal*2
1750 recalculateEdge()
1760 endproc
1770 '
1780 ' "Recalculate left/right edge"
1790 '
1800 proc recalculateEdge()
1810 local i
1820 leftEdge = 8
1830 i = 0:while colHeight(i) = 0:i = i + 1:leftEdge = leftEdge-24:wend
1840 rightEdge = 320-8-7*24
1850 i = 7:while colHeight(i) = 0:i = i - 1:rightEdge = rightEdge+24:wend
1860 endproc
每每每每

