1000 '
1010 ' "Simplified Space Invaders Game"
1020 '
1030 cls:bitmap on:sprites on:bitmap clear 0
1040 skillLevel = 0:score = 0:lives = 3
1050 defineVariables():text "SCORE<1>" dim 1 colour $FF to 136,1
1060 resetlevel():resetPlayer():displayScore()
1070 repeat
1080 if event(moveInvadersEvent,invaderSpeed) then moveInvaders()
1090 if event(movePlayerEvent,3) then movePlayer():if yBullet >= 0 then moveBullet()
1100 if event(moveMissileEvent,2) then moveMissile()
1110 until lives = 0
1120 end
1130 '
1140 ' "Display the score"
1150 '
1160 proc displayScore()
1170 local a$:a$ = right$("00000"+str$(score),6)
1180 text a$ dim 1 colour $FF,4 to 144,10
1190 text "LIVES "+str$(lives) colour $1C,4 to 10,230
1200 endproc
1210 '
1220 ' "Move the player"
1230 '
1240 proc movePlayer()
1250 xPlayer = min(304,max(16,xPlayer+joyx(0)<<2))
1260 sprite 63 image 6 to xPlayer,220
1270 if joyb(0) & yBullet < 0 then xBullet = xPlayer:yBullet = 200
1280 endproc
1290 '
1300 ' "Flash player for 2 seconds"
1310 '
1320 proc flashPlayer()
1330 local tEnd:tEnd = timer()+70*2
1340 repeat
1350 if timer() & 8
1360 sprite 63 image 6
1370 else
1380 sprite 63 off
1390 endif
1400 until timer() > tEnd
1410 endproc
1420 '
1430 ' "Move the player bullet"
1440 '
1450 proc moveBullet()
1460 local xo
1470 yBullet = yBullet - 10
1480 if yBullet < 0
1490 sprite 62 off
1500 else
1510 sprite 62 image 11 to xBullet,yBullet
1520 xo = xBullet - xInvaders + 8
1530 if xo >= 0 & xo < 8*24 & xo % 24 < 16 then checkHit(xo \ 24)
1540 endif
1550 endproc
1560 '
1570 ' "Move current missile"
1580 '
1590 proc moveMissile()
1600 local r
1610 currentMissile = currentMissile + 1:if currentMissile > missileCount then currentMissile = 1
1620 if yMissile(currentMissile) < 0
1630 r = random(8)
1640 if colHeight(r) > 0 & (random()& 3) = 0
1650 xMissile(currentMissile) = xInvaders + 24 * r
1660 yMissile(currentMissile) = yInvaders + 24 * colHeight(r) - 24
1670 endif
1680 else
1690 yMissile(currentMissile) = yMissile(currentMissile) + 8
1700 if yMissile(currentMissile) > 220
1710 if abs(xMissile(currentMissile)-xPlayer) < 12
1720 lives = lives - 1:displayScore()
1730 flashPlayer()
1740 if lives > 0 then resetLevel()
1750 endif
1760 sprite currentMissile+50 off
1770 yMissile(currentMissile) = -1
1780 else
1790 sprite currentMissile+50 image 9 to xMissile(currentMissile),yMissile(currentMissile)
1800 endif
1810 endif
1820 endproc
1830 '
1840 ' "Check if column hit"
1850 '
1860 proc checkHit(col)
1870 yo = abs(yInvaders + (colHeight(col)-1)*24 - yBullet)
1880 if yo < 12 & colHeight(col) <> 0
1890 sprite col*5+colHeight(col)-1 off
1900 sprite 61 image 7 to col*24+xInvaders,(colHeight(col)-1)*24+yInvaders
1910 yBullet = -1:sprite 62 off
1920 score = score+(6-colHeight(col))*10:displayScore()
1930 colHeight(col) = colHeight(col)-1
1940 invTotal = invTotal - 1
1950 if invTotal > 0 & colHeight(col) = 0 then recalculateEdge()
1960 if invTotal = 0
1970 skillLevel = (skillLevel+1) % 10
1980 resetlevel()
1990 endif
2000 recalculateSpeed()
2010 endif
2020 endproc
2030 '
2040 ' "Move invaders across/down"
2050 '
2060 proc moveInvaders()
2070 xInvaders = xInvaders + xiInvaders
2080 if xInvaders < leftEdge
2090 xInvaders = leftEdge
2100 yInvaders = yInvaders + abs(xiInvaders)
2110 xiInvaders = -xiInvaders
2120 endif
2130 if xInvaders > rightEdge
2140 xInvaders = rightEdge
2150 yInvaders = yInvaders + abs(xiInvaders)
2160 xiInvaders = -xiInvaders
2170 endif
2180 drawInvaders(xInvaders,yInvaders)
2190 endproc
2200 '
2210 ' "Draw invaders sprites at correct position"
2220 '
2230 proc drawInvaders(xPos,yPos)
2240 local x,y,s
2250 altGraphic = 1 - altGraphic
2260 for x = 0 to 7
2270 s = x * 5
2280 if colHeight(x) > 0
2290 for y = 0 to colHeight(x)-1
2300 sprite s image graphic(y)+altGraphic to xPos+x*24,yPos+y*24
2310 s = s + 1
2320 next
2330 sprite 61 off
2340 endif
2350 next
2360 endproc
2370 '
2380 ' "Set up variables"
2390 '
2400 proc defineVariables()
2410 local i:missileCount = 4
2420 dim colheight(7),graphic(4),xMissile(missileCount),yMissile(missileCount)
2430 for i = 0 to 4:graphic(i) = i % 3 * 2:next:altGraphic = 0
2440 endproc
2450 '
2460 ' "Reset the player"
2470 '
2480 proc resetPlayer()
2490 xPlayer = 160:xBullet = 0:yBullet = -1
2500 movePlayerEvent = 0
2510 endproc
2520 '
2530 ' "Set up new level"
2540 '
2550 proc resetlevel()
2560 local i
2570 for i = 0 to 7:colHeight(i) = 5:next
2580 for i = 1 to missileCount:yMissile(i) = -1:sprite i+50 off:next
2590 invTotal = 5*8
2600 xInvaders = 160-7*12:yInvaders = 26+skillLevel*8:xiInvaders = 8
2610 drawInvaders(xInvaders,yInvaders)
2620 invaderSpeed = 4+invTotal*2
2630 moveInvadersEvent = 0:currentMissile = 0
2640 recalculateEdge():recalculateSpeed()
2650 endproc
2660 '
2670 ' "Recalaulate speed"
2680 '
2690 proc recalculateSpeed()
2700 invaderSpeed = 2+invTotal*3\2
2710 endproc
2720 '
2730 ' "Recalculate left/right edge"
2740 '
2750 proc recalculateEdge()
2760 local i
2770 leftEdge = 8
2780 i = 0:while colHeight(i) = 0:i = i + 1:leftEdge = leftEdge-24:wend
2790 rightEdge = 320-8-7*24
2800 i = 7:while colHeight(i) = 0:i = i - 1:rightEdge = rightEdge+24:wend
2810 endproc
每每每每

