1000 '
1010 ' "Simple Space Invaders Game"
1020 '
1030 cls:bitmap on:sprites on:bitmap clear 0
1040 defineVariables()
1050 resetlevel()
1060 repeat
1070 if event(moveInvadersEvent,invaderSpeed) then moveInvaders()
1080 until false
1090 end:tax
1100 '
1110 ' "Move invaders across/down"
1120 '
1130 proc moveInvaders()
1140 xInvaders = xInvaders + xiInvaders
1150 if xInvaders < leftEdge
1160 xInvaders = leftEdge
1170 yInvaders = yInvaders + abs(xiInvaders)
1180 xiInvaders = -xiInvaders
1190 endif
1200 if xInvaders > rightEdge
1210 xInvaders = rightEdge
1220 yInvaders = yInvaders + abs(xiInvaders)
1230 xiInvaders = -xiInvaders
1240 endif
1250 drawInvaders(xInvaders,yInvaders)
1260 endproc
1270 '
1280 ' "Draw invaders sprites at correct position"
1290 '
1300 proc drawInvaders(xPos,yPos)
1310 local x,y,s
1320 altGraphic = 1 - altGraphic
1330 for x = 0 to 7
1340 s = x * 5
1350 if colHeight(x) > 0
1360 for y = 0 to colHeight(x)-1
1370 sprite s image graphic(y)+altGraphic to xPos+x*24,yPos+y*24
1380 s = s + 1
1390 next
1400 endif
1410 next
1420 endproc
1430 '
1440 ' "Calculate invader movement rate"
1450 '
1460 proc calculateSpeed()
1470 invaderSpeed = 4+invTotal*2
1480 endproc
1490 '
1500 ' "Set up variables"
1510 '
1520 proc defineVariables()
1530 local i
1540 dim colheight(7),graphic(4)
1550 for i = 0 to 4:graphic(i) = i % 3 * 2:next:altGraphic = 0
1560 endproc
1570 '
1580 ' "Set up new level"
1590 '
1600 proc resetlevel()
1610 local i
1620 for i = 0 to 7:colHeight(i) = 5:next
1630 invTotal = 5*8
1640 leftEdge = 8:rightEdge = 320-8-7*24
1650 xInvaders = 160-7*12:yInvaders = 26:xiInvaders = 8
1660 drawInvaders(xInvaders,yInvaders)
1670 calculateSpeed()
1680 endproc
每每每每

