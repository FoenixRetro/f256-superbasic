1000 cls:sprites on:bitmap on:bitmap clear 0
1010 initialise()
1020 newLevel()
1030 repeat
1040 if event(moveEnemies,4)
1050 updateEnemies()
1060 n = random(mcount):if remain(n) = 0 then launch(n)
1070 endif
1080 if event(moveMissiles,6) then moveMissiles()
1090 if event(movePlayer,3) then movePlayer()
1100 until False
1110 end
1120 proc initialise()
1130 xSize = 14:ySize = 11:mCount = 8
1140 xOrg = 160-xSize*8:yOrg = 140-ySize*8
1150 dim x(mCount-1),y(mCount-1),xi(mCount-1),yi(mCount-1),remain(mCount-1)
1160 endproc
1170 proc movePlayer()
1180 local x,y
1190 x = joyx(0):y = joyy(0)
1200 if x <> 0 & (yPlayer & 15) = 0 then xiPlayer = x * 4:yiPlayer = 0:iPlayer = 1-x
1210 if y <> 0 & (xPlayer & 15) = 0 then yiPlayer = y * 4:xiPlayer = 0:iPlayer = 2-y
1220 xPlayer = min((xSize-1) << 4,max(0,xPlayer + xiPlayer))
1230 yPlayer = min((ySize-1) << 4,max(0,yPlayer + yiPlayer))
1240 sprite 50 image iPlayer to xOrg+xPlayer,yOrg+yPlayer
1250 endproc
1260 proc launch(n)
1270 if random() & 1:horizontalLaunch(n):else:verticalLaunch(n):endif
1280 endproc
1290 proc verticalLaunch(n)
1300 x(n) = xOrg-16:y(n) = yOrg+((yFire+8) & $F0):xi(n) = 4:yi(n) = 0
1310 remain(n) = abs((xSize*16+16) / xi(n))
1320 if random() & 1 then x(n) = x(n) + remain(n)*xi(n):xi(n) = -xi(n)
1330 sprite n image 11 to x(n),y(n)
1340 endproc
1350 proc horizontalLaunch(n)
1360 y(n) = yOrg-16:x(n) = xOrg+((xFire+8) & $F0):yi(n) = 4:xi(n) = 0
1370 remain(n) = abs((ySize*16+16) / yi(n))
1380 if random() & 1 then y(n) = y(n) + remain(n)*yi(n):yi(n) = -yi(n)
1390 sprite n image 12 to x(n),y(n)
1400 endproc
1410 proc moveMissiles()
1420 local i
1430 for i = 0 to mCount-1
1440 if remain(i) > 0
1450 x(i) = x(i)+xi(i):y(i) = y(i)+yi(i)
1460 remain(i) = remain(i)-1
1470 if remain(i) > 0:sprite i to x(i),y(i):else:sprite i off:endif
1480 endif
1490 next
1500 endproc
1510 proc newLevel()
1520 bitmap clear 0
1530 local i
1540 for i = 0 to mCount-1:remain(i) = 0:next
1550 drawBackground():updateEnemies()
1560 xPlayer = xSize\2*16:yPlayer = ySize\2*16:iPlayer = 0:xiPlayer = 0:yiPlayer = 0
1570 sprite 50 image iPlayer to xOrg+xPlayer,yOrg+yPlayer
1580 endproc
1590 proc drawBackground()
1600 local x,y:line colour $25
1610 for x = 0 to xSize-1:line xOrg+x*16,yOrg-8 by 0,ySize*16:next
1620 for y = 0 to ySize-1:line xOrg-8,yOrg+y*16 by xSize*16,0:next
1630 rect colour $E0 outline xOrg-24,yOrg-24 by xSize*16+32,ySize*16+32
1640 rect colour $FF outline xOrg-25,yOrg-25 by xSize*16+34,ySize*16+34
1650 endproc
1660 proc updateEnemies()
1670 local t:t = timer()
1680 xFire = abs((t % (xSize << 5))-(xSize << 4))
1690 sprite 10 image 5 to xOrg+xFire,yOrg-16
1700 sprite 11 image 7 to xOrg+xFire,yOrg+ySize<<4
1710 yFire = abs((t % (ySize << 5))-(ySize << 4))
1720 sprite 12 image 4 to xOrg-16,yOrg+yFire
1730 sprite 13 image 6 to xOrg+xSize<<4,yOrg+yFire
1740 endproc
每每每每

