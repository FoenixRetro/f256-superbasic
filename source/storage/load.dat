100 '
110 ' "Squash, gotta start somewhere."
120 '
130 bitmap on:sprites on:bitmap clear 0:cls
140 score = 0:showscore()
150 upper = 20:lower = 230:showframe()
160 batx = 32:baty = 120:batSize = 30:drawbat()
165 ballx = 64:bally = 120:ballxi = 2:ballyi = 2:drawball()
170 repeat
180 movebat():moveball()
190 until false


990  '
991  ' "Update the score"
992  '
1000 proc showscore()
1010 local a$:a$ = right$("000"+str$(score),4)
1015 rect solid colour 0 from 256,2 to 319,18
1020 text a$ dim 2 colour $FE,0 to 256,2
1030 endproc
1040 '
1050 ' "Draw framework"
1055 '
1060 proc showframe()
1070 line 0,upper colour $FF to 319,upper to 319,lower to 0,lower
1100 endproc
1110 '
1120 ' "Draw/Erase the bat"
1130 '
1140 proc drawbat()
1150 rect solid colour $FC,1 from batx-4,baty-batSize\2 to batx,baty+batSize\2
1160 endproc
1200 '
1210 ' "Move the bat"
1220 '
1230 proc movebat()
1240 local yi:yi = joyy(0)*4
1250 if yi <> 0
1260  	drawbat()
1270 	baty = baty + yi
1272 	if baty-batSize\2 < upper | baty+batSize\2 > lower then baty = baty - yi
1280 	drawbat()
1290 endif
1300 endproc
1310 '
1320 ' "Move the ball"
1330 '
1400 proc moveball()
1405 drawball()
1410 ballx = ballx + ballxi:bally = bally+ballyi
1411 if bally < upper | bally > lower then ballyi = -ballyi
1412 if ballx > 314 then ballxi = -ballxi:score = score + 1:showscore()
1415 if ballx < batx-16 then ballxi = -ballxi
1417 drawball()
1420 endproc
1430
1440 '
1450 ' 	"Draw the ball"
1460 '
1500 proc drawball()
1510 rect solid colour $1F,1 from ballx-2,bally-2 to ballx+2,bally+2
1520 endproc
ÿÿÿÿ